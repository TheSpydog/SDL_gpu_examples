#version 450

layout (local_size_x = 8, local_size_y = 8, local_size_z = 1) in;

layout (set = 0, binding = 0, rgba32f) uniform readonly image2D inImage;
layout (set = 0, binding = 1, rgba16) uniform writeonly image2D outImage;

vec3 hable_tonemap_partial(vec3 x)
{
    float A = 0.15f;
    float B = 0.50f;
    float C = 0.10f;
    float D = 0.20f;
    float E = 0.02f;
    float F = 0.30f;
    return ((x*(A*x+C*B)+D*E)/(x*(A*x+B)+D*F))-E/F;
}

vec3 hable_filmic(vec3 v)
{
    float exposure_bias = 2.0f;
    vec3 curr = hable_tonemap_partial(v * exposure_bias);

    vec3 W = vec3(11.2f);
    vec3 white_scale = vec3(1.0f) / hable_tonemap_partial(W);
    return curr * white_scale;
}

void main()
{
	ivec2 coord = ivec2(gl_GlobalInvocationID.xy);
	vec4 inPixel = imageLoad(inImage, coord);

	vec3 outColor = hable_filmic(inPixel.rgb);
	imageStore(outImage, coord, vec4(outColor, 1.0));
}
